apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.6"

android {
    resourcePrefix "zz_" //随意填写

    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 7
        versionName "1.6"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:recyclerview-v7:27.0.1'
}



task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = "me.zhouzhuo.zzimagebox"            // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'me.zhouzhuo.zzimagebox' //自己定义的组名
            artifactId 'zz-image-box'

            name 'zz-image-box'
            description 'A powerful image container for adding and removing images.'
            url 'https://github.com/zhouzhuo810/ZzImageBox'
            inceptionYear '2016'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/zhouzhuo810/ZzImageBox.git'
                url 'https://github.com/zhouzhuo810/ZzImageBox'

            }
            developers {
                developer {
                    name 'zhouzhuo'
                    email 'admin@zhouzhuo810.me'
                }
            }
        }
    }
}

// Bintray

//获取local.propertes的信息
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        //填入 bintray 上对应的 package 信息
        repo = 'maven'
        name = 'zz-image-box'
        vcsUrl = 'https://github.com/zhouzhuo810/ZzImageBox.git'
        websiteUrl = 'https://github.com/zhouzhuo810/ZzImageBox'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/zhouzhuo810/ZzImageBox/issues'
        publicDownloadNumbers = true
        version {
            name = '1.0.6'
            desc = 'zz-image-box release'
            vcsTag = '1.0.6'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}
